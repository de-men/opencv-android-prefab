plugins {
    id 'com.android.library'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def sdkDir = properties.getProperty('sdk.dir')

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 2
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                // Shared runtime for shared libraries
                arguments "-DANDROID_STL=c++_shared",
                        "-DANDROID_HOME=$sdkDir",
                        "-DCMAKE_BUILD_TYPE=Release",
                        "-DANDROID_NATIVE_API_LEVEL=21",
                        "-DWITH_CUDA=OFF",
                        "-DWITH_MATLAB=OFF",
                        "-DBUILD_ANDROID_EXAMPLES=OFF",
                        "-DBUILD_DOCS=OFF",
                        "-DBUILD_PERF_TESTS=OFF",
                        "-DBUILD_TESTS=OFF",
                        "-DBUILD_SHARED_LIBS=ON",
                        "-DBUILD_opencv_core=ON",
                        "-DBUILD_opencv_imgproc=ON",
                        "-DBUILD_opencv_flann=ON",
                        "-DBUILD_opencv_ml=ON",
                        "-DBUILD_opencv_imgcodecs=ON",
                        "-DBUILD_opencv_features2d=ON",
                        "-DBUILD_opencv_gapi=ON",
                        "-DBUILD_opencv_dnn=ON",
                        "-DBUILD_opencv_photo=ON",
                        "-DBUILD_opencv_video=ON",
                        "-DBUILD_opencv_calib3d=ON",
                        "-DBUILD_opencv_highgui=ON",
                        "-DBUILD_opencv_videoio=ON",
                        "-DBUILD_opencv_objdetect=ON",
                        "-DBUILD_opencv_stitching=ON",
                        "-DBUILD_opencv_java=OFF",
                        "-DBUILD_opencv_python=OFF",
                        "-DBUILD_FAT_JAVA_LIB=OFF",
                        "-BUILD_opencv_js_bindings_generator=OFF",
                        "-BUILD_opencv_objc_bindings_generator=OFF",
                        "-BUILD_opencv_java_bindings_generator=OFF"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        // Encapsulates your CMake build configurations.
        cmake {
            // Provides a relative path to your CMake build script.
            path "../../opencv/CMakeLists.txt"
        }
    }

    // Enable generation of Prefab packages and include them in the library's AAR
    buildFeatures {
        prefabPublishing true
    }

    // Include the "opencv" module from the native build system in the AAR,
    // and export the headers in src/main/cpp/include to its comsumers
    prefab {
        opencv_core {
            headers "src/main/cpp/opencv_core/include"
        }

        opencv_imgproc {
            headers "src/main/cpp/opencv_imgproc/include"
        }
        opencv_flann {
            headers "src/main/cpp/opencv_flann/include"
        }
        opencv_ml {
            headers "src/main/cpp/opencv_ml/include"
        }

        opencv_imgcodecs {
            headers "src/main/cpp/opencv_imgcodecs/include"
        }
        opencv_features2d {
            headers "src/main/cpp/opencv_features2d/include"
        }
        opencv_gapi {
            headers "src/main/cpp/opencv_gapi/include"
        }
        opencv_dnn {
            headers "src/main/cpp/opencv_dnn/include"
        }
        opencv_photo {
            headers "src/main/cpp/opencv_photo/include"
        }
        opencv_video {
            headers "src/main/cpp/opencv_video/include"
        }

        opencv_calib3d {
            headers "src/main/cpp/opencv_calib3d/include"
        }
        opencv_highgui {
            headers "src/main/cpp/opencv_highgui/include"
        }
        opencv_videoio {
            headers "src/main/cpp/opencv_videoio/include"
        }

        opencv_objdetect {
            headers "src/main/cpp/opencv_objdetect/include"
        }
        opencv_stitching {
            headers "src/main/cpp/opencv_stitching/include"
        }
    }

    // Avoid packing the unnecessary libraries into final AAR. For details
    // refer to https://issuetracker.google.com/issues/168777344#comment5
    // Note that if your AAR also contains Java/Kotlin APIs, you should not
    // exclude libraries that are used by those APIs.
    packagingOptions {
        exclude("**/libopencv_core.so")
        exclude("**/libopencv_imgproc.so")
        exclude("**/libopencv_flann.so")
        exclude("**/libopencv_ml.so")
        exclude("**/libopencv_imgcodecs.so")
        exclude("**/libopencv_features2d.so")
        exclude("**/libopencv_gapi.so")
        exclude("**/libopencv_dnn.so")
        exclude("**/libopencv_photo.so")
        exclude("**/libopencv_video.so")
        exclude("**/libopencv_calib3d.so")
        exclude("**/libopencv_highgui.so")
        exclude("**/libopencv_videoio.so")
        exclude("**/libopencv_objdetect.so")
        exclude("**/libopencv_stitching.so")
        exclude("**/libc++_shared.so")
    }
}

apply plugin: "com.vanniktech.maven.publish"

allprojects {
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
